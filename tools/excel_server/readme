圆管质量计算公式
=LET(
    OuterDiameter, C6,
    WallThickness, D6,
    Length, B6,
    OuterRadius, OuterDiameter / 2,
    InnerRadius, OuterDiameter / 2 - WallThickness,
    CrossSectionArea, (PI() * (OuterRadius^2 - InnerRadius^2)),
    Volume, CrossSectionArea * Length,
    Density, 7.85,
    Weight, Volume * Density 
    ROUND(Weight, 2)
)

钢板计算公式
=LET(
    clean,
    SUBSTITUTE(
        SUBSTITUTE(
            SUBSTITUTE(
                SUBSTITUTE(
                    SUBSTITUTE(
                        SUBSTITUTE(
                            SUBSTITUTE(
                                SUBSTITUTE(
                                    SUBSTITUTE(C3,"x","*"),
                                "X","*"),
                            "mm",""),
                        "-","*"),
                    "FP",""),
                "_","*"),
            " ","*"),
        "D",""),
    "PL",""),
    parts, SUBSTITUTE(clean,"*",REPT(" ",99)),
    a, IFERROR(VALUE(TRIM(MID(parts,1,99))),0),
    b, IFERROR(VALUE(TRIM(MID(parts,100,99))),0),
    c, IFERROR(VALUE(TRIM(MID(parts,199,99))),0),
    IF(AND(a>0,b>0,c>0), ROUND(a*b*c/1000000*7.85,2), "")
)
矩形管质量公式
=LET(
    H, B6,
    W, C6,
    T, D6,
    L, E6,                    
    Area_mm2, (H*W) - ((H-2*T)*(W-2*T)),  
    Volume_mm3, Area_mm2 * L,             
    Weight_kg, Volume_mm3 * 7.85 / 1000000, 
    ROUND(Weight_kg, 2)                   
)